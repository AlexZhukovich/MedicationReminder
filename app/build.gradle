apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
//apply plugin: 'jacoco-android'
apply plugin: 'jacoco'

jacoco {
    toolVersion '0.7.9'
}

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"
    defaultConfig {
        applicationId "com.alexzh.medicationreminder"
        minSdkVersion 18
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        vectorDrawables {
            useSupportLibrary = true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
//    testOptions {
//        unitTests {
//            includeAndroidResources = true
//            returnDefaultValues     = true
////            execution 'ANDROID_TEST_ORCHESTRATOR'
//
//            all {
//                jacoco {
//                    includeNoLocationClasses true
//                }
//            }
//        }
//    }
    sourceSets {
        def commonTestDir = 'src/commonTest/java'
        test {
            java.srcDir commonTestDir
        }
        androidTest {
            java.srcDir commonTestDir
        }
    }
}

def coverageSourceDirs = ['src/main/java']

task jacocoTestReport(type : JacocoReport, dependsOn : 'testDebugUnitTest') {
    group       = 'Reporting'
    description = 'Generate JaCoCo coverage reports'

    reports {
        xml.enabled  = true
        html.enabled = true
    }

    classDirectories = fileTree(
            dir      : 'build/intermediates/classes/debug',
            excludes : [
                    '**/R.class',
                    '**/R$*.class',
                    '**/*$ViewInjector*.*',
                    '**/*$ViewBinder*.*',
                    '**/BuildConfig.*',
                    '**/Manifest*.*',
                    '**/*RealmProxy.*',
                    '**/*ColumnInfo.*',
                    '**/*RealmModule*.*',
                    '**/AutoValue_*.*',
                    '**/Dagger*.*',
                    '**/*Module_Provide*Factory.*',
                    '**/*_Factory.*',
                    '**/*_MembersInjector.*',
                    '**/*_LifecycleAdapter.*'
            ]
    )

    sourceDirectories = files(coverageSourceDirs)
    executionData     = fileTree(
            dir     : "$buildDir",
            include : [ 'jacoco/testDebugUnitTest.exec', 'outputs/code-coverage/connected/*coverage.ec' ]
    )

    doFirst {
        files('build/intermediates/classes/debug').getFiles().each { file ->
            if (file.name.contains('$$')) {
                file.renameTo(file.path.replace('$$', '$'))
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    // Android Support Libraries
    implementation "com.android.support:design:$support_library_version"
    implementation "com.android.support:appcompat-v7:$support_library_version"
    implementation "com.android.support:recyclerview-v7:$support_library_version"
    implementation "com.android.support:preference-v14:$support_library_version"

    // Constraint layout
    implementation "com.android.support.constraint:constraint-layout:$constraint_layout_version"

    // RxJava and RxAndroid
    implementation "io.reactivex.rxjava2:rxjava:$rxjava_version"
    implementation "io.reactivex.rxjava2:rxandroid:$rxandroid_version"

    // Android Architecture Core
    androidTestImplementation "android.arch.core:core-testing:$architecture_comp_version"

    // Room
    implementation "android.arch.persistence.room:runtime:$architecture_comp_version"
    kapt "android.arch.persistence.room:compiler:$architecture_comp_version"
    testImplementation "android.arch.persistence.room:testing:$architecture_comp_version"
    implementation "android.arch.persistence.room:rxjava2:$architecture_comp_version"

    // Glide
    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    // Timber
    implementation "com.jakewharton.timber:timber:$timber_version"

    // JUnit
    testImplementation "junit:junit:$junit_version"

    // Robolectric
    testImplementation "org.robolectric:robolectric:$robolectric_version"

    // Mockito
    androidTestImplementation "org.mockito:mockito-android:$mockito_android_version"
    testImplementation "com.nhaarman:mockito-kotlin-kt1.1:$mockito_kotlin_version"
    androidTestImplementation "com.nhaarman:mockito-kotlin-kt1.1:$mockito_kotlin_version"

    // Test Runner
    androidTestImplementation "com.android.support.test:runner:$test_runner"

    // Espresso
    androidTestImplementation ("com.android.support.test.espresso:espresso-core:$espresso_version", {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude module: 'recyclerview-v7'
    })
    androidTestImplementation ("com.android.support.test.espresso:espresso-contrib:$espresso_version", {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude module: 'recyclerview-v7'
    })
//    androidTestImplementation ("com.android.support.test.espresso:espresso-intents:$espresso_version", {
//        exclude group: 'com.android.support', module: 'support-annotations'
//        exclude group: 'com.android.support', module: 'appcompat'
//        exclude group: 'com.android.support', module: 'support-v4'
//        exclude group: 'com.android.support', module: 'design'
//        exclude module: 'recyclerview-v7'
//    })

    // UiAutomator
    androidTestImplementation "com.android.support.test.uiautomator:uiautomator-v18:$uiautomator_version"

//    androidTestUtil 'com.android.support.test:orchestrator:1.0.1'
}
